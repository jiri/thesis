use std::char;

// Helpers {{{

    digit = #quiet<[0-9]> / #expected("a decimal digit")

    hexdigit = #quiet<[0-9a-fA-F]> / #expected("a hexadecimal digit")

    bindigit = #quiet<[01]> / #expected("a binary digit")

    identifier = #quiet<[a-zA-Z_][a-zA-Z_0-9]*> / #expected("an identifier")

    ws = #quiet<[ \t]>

    comma = #quiet<ws* "," ws*> / #expected("a comma")

// }}}

// Operands {{{

    label -> Label
        = #quiet<i:$("."? identifier) { Label::from(i) }> / #expected("a label")

    address -> Address
        = l:label { Address::Label(l) } / u:u16 { Address::Immediate(u) }

    u16_dec -> u16
        = s:$(digit+) {? u16::from_str_radix(s, 10).map_err(|_| "a 16-bit decimal constant") }

    u16_hex -> u16
        = "0x" s:$(hexdigit+) {? u16::from_str_radix(s, 16).map_err(|_| "a 16-bit hexadecimal constant") }

    u16_bin -> u16
        = "0b" s:$(bindigit+) {? u16::from_str_radix(s, 2).map_err(|_| "a 16-bit binary constant") }

    u16 -> u16
        = #quiet<u16_hex / u16_bin / u16_dec> / #expected("a word literal")

    u8_dec -> u8
        = s:$(digit+) {? u8::from_str_radix(s, 10).map_err(|_| "an 8-bit decimal constant") }

    u8_hex -> u8
        = "0x" s:$(hexdigit+) {? u8::from_str_radix(s, 16).map_err(|_| "an 8-bit hexadecimal constant") }

    u8_bin -> u8
        = "0b" s:$(bindigit+) {? u8::from_str_radix(s, 2).map_err(|_| "an 8-bit binary constant") }

    u8_char -> u8
        = "'" c:asciiChar "'" { c as u8 }

    u8 -> u8
        = #quiet<u8_hex / u8_bin / u8_char / u8_dec> / #expected("a byte literal")

    hi_addr -> Value
        = "hi"i ws* "(" ws* a:address ws* ")" { Value::Addr(a, Nibble::High) }

    lo_addr -> Value
        = "lo"i ws* "(" ws* a:address ws* ")" { Value::Addr(a, Nibble::Low) }

    value -> Value
        = #quiet<u:u8 { Value::Immediate(u) } / hi_addr / lo_addr> / #expected("a value")

    register -> Register
        = #quiet<"R"i v:$(digit*<1,2>) {? Register::new(v.parse::<u8>().unwrap()) }> / #expected("a register")

    string -> String
        = #quiet<'"' s:asciiChar* '"' { s.into_iter().collect() }> / #expected("a string literal")

    asciiChar -> char
        = #quiet<quotedChar / escapeSequence / hexEscapeSequence> / #expected("an ASCII character")

    quotedChar -> char
        = !('"' / "\\") c:$. { c.chars().next().unwrap() }

    escapeSequence -> char
        = "\\" !(digit / "x" / "u") c:$(.) {
            match c.chars().next().unwrap() {
                'n' => '\n',
                'r' => '\r',
                't' => '\t',
                 x  => x,
            }
        }

    hexEscapeSequence -> char
        = "\\x" value:$(hexdigit hexdigit) { char::from_u32(u32::from_str_radix(value, 16).unwrap()).unwrap() }

    serializable -> Serializable
        = b:u8 { Serializable::Byte(b) } / s:string { Serializable::String(s) }

// }}}

// Instructions {{{

    // Classes {{{

        nullary<M>
            = m:$(M) { Instruction::Nullary(*OPCODES.get(m).unwrap()) }

        unaryReg<M>
            = m:$(M) ws+ r:register { Instruction::UnaryReg(*OPCODES.get(m).unwrap(), r) }

        unaryAddr<M>
            = m:$(M) ws+ a:address { Instruction::UnaryAddr(*OPCODES.get(m).unwrap(), a) }

        binaryRegIm<M>
            = m:$(M) ws+ r:register comma v:value { Instruction::BinaryRegIm(*OPCODES.get(m).unwrap(), r, v) }

        binaryRegReg<M>
            = m:$(M) ws+ rDst:register comma rSrc:register { Instruction::BinaryRegReg(*OPCODES.get(m).unwrap(), rDst, rSrc) }

        binaryRegAddr<M>
            = m:$(M) ws+ r:register comma a:address { Instruction::BinaryRegAddr(*OPCODES.get(m).unwrap(), r, a) }


    // }}}

    // Special {{{

        db -> Instruction
            = "db" ws+ vs:serializable ** comma { Instruction::Db(vs) }

        ds -> Instruction
            = "ds" ws+ len:u16 { Instruction::Ds(len) }

        org -> Instruction
            = "org" ws+ addr:u16 { Instruction::Org(addr) }

        include -> Instruction
            = "include" ws+ path:string { Instruction::Include(path) }

        special -> Instruction
            = db / ds / org / include

    // }}}

    // Utility {{{

        nop -> Instruction
            = nullary<"nop">

        sleep -> Instruction
            = nullary<"sleep">

        break -> Instruction
            = nullary<"break">

        sei -> Instruction
            = nullary<"sei">

        sec -> Instruction
            = nullary<"sec">

        sez -> Instruction
            = nullary<"sez">

        cli -> Instruction
            = nullary<"cli">

        clc -> Instruction
            = nullary<"clc">

        clz -> Instruction
            = nullary<"clz">

        utility -> Instruction
            = nop / sleep / break / sei / sec / sez / cli / clc / clz

    // }}}

    // Arithmetics {{{

        add -> Instruction
            = binaryRegReg<"add">

        adc -> Instruction
            = binaryRegReg<"adc">

        sub -> Instruction
            = binaryRegReg<"sub">

        sbc -> Instruction
            = binaryRegReg<"sbc">

        inc -> Instruction
            = unaryReg<"inc">

        dec -> Instruction
            = unaryReg<"dec">

        and -> Instruction
            = binaryRegReg<"and">

        or -> Instruction
            = binaryRegReg<"or">

        xor -> Instruction
            = binaryRegReg<"xor">

        cp -> Instruction
            = binaryRegReg<"cp">

        cpi -> Instruction
            = binaryRegIm<"cpi">

        arithmetic -> Instruction
            = add / adc / sub / sbc / inc / dec / and / or / xor / cp / cpi

    // }}}

    // Flow control {{{

        jmp -> Instruction
            = unaryAddr<"jmp">

        call -> Instruction
            = unaryAddr<"call">

        ret -> Instruction
            = nullary<"ret">

        reti -> Instruction
            = nullary<"reti">

        brc -> Instruction
            = unaryAddr<"brc">

        brnc -> Instruction
            = unaryAddr<"brnc">

        brz -> Instruction
            = unaryAddr<"brz">

        brnz -> Instruction
            = unaryAddr<"brnz">

        control -> Instruction
            = jmp / call / reti / ret / brc / brnc / brz / brnz

    // }}}

    // Load / Store {{{

        mov -> Instruction
            = binaryRegReg<"mov">

        ldi -> Instruction
            = binaryRegIm<"ldi">

        ld -> Instruction
            = unaryReg<"ld">

        st -> Instruction
            = unaryReg<"st">

        push -> Instruction
            = unaryReg<"push">

        pop -> Instruction
            = unaryReg<"pop">

        lpm -> Instruction
            = unaryReg<"lpm">

        in -> Instruction
            = binaryRegIm<"in">

        out -> Instruction
            = binaryRegIm<"out">

        loadstore -> Instruction
            = mov / ldi / ld / st / push / pop / lpm / in / out

    // }}}

// }}}

/// Basic constructs {{{

    label_definition -> Label
        = l:label ws* ":" { l }

    instruction -> Instruction
        = special / loadstore / arithmetic / control / utility

    comment = #quiet<";" [^\n]*>

    pub parse_line -> Line
        = ws* l:label_definition? ws* i:instruction? ws* comment? { Line { label: l, instruction: i } }

// }}}
